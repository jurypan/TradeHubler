//@version=5
strategy("STR4.2", overlay=true, process_orders_on_close = true, calc_on_every_tick = true, default_qty_type = strategy.percent_of_equity, default_qty_value = 100, pyramiding = 100, currency = currency.NONE, initial_capital = 10000000, close_entries_rule="ANY")
import TradingView/ta/5



//INPUTS
i_showStrategy              = input.bool(true,title="Show Strategy",group = "General") 
i_showRiskReward            = input.bool(true,title="Show Risk/Reward Area",group = "General") 
i_takeLongOrShort           = input.string("Long", title="Direction",group = "General", options = ["Long", "Short"])

i_isCloseTime               = input.string("End of session", title="Close trades",group = "Timing", options = ["End of session", "Friday end of session", "None"])
i_timeOpenAM                = input.int(0800, title="Open Time",group = "Timing")
i_timeClosePM               = input.int(1455, title="Close Time",group = "Timing")

i_pyramiding                = input.bool(false,title = "Pyramiding?", group = "Risk / Reward")
i_positionSize              = input.string("Fixed 1%", title = "Position Size", options = ["Fixed 1%", "Compound 1%", "1 Unit", "Auto"], group = "Risk / Reward")
i_rewardRatio               = input.float(5.0, title = "Reward Ratio", group = "Risk / Reward") 
i_moveSLtoBEafterR          = input.float(1.0,title="Move SL to BE after xR",group = "Risk / Reward") 
i_minRisk                   = input.float(5.0, title="Min risk in ticks",group = "Risk / Reward") 
i_cancellimitNewSignal      = input.bool(false, title="Cancel stop order when new signal",group = "Risk / Reward") 


i_pcLicenseID               = input.string("692803787",title="License ID",group = "Account")


//VARS
var int id = 0
var float longTP = na, var float longSL = na, var float longEntryPrice = na
var float shortTP = na, var float shortSL = na, var float shortEntryPrice = na
var float risk = na

// Variables to accumulate highs and count bars
var float sumSignalCandle = 0.0
var int countSignalCandle = 0

//FUNCTIONS
bullishOutsideBar() => 
    close > high[1] and low < low[1]

bearishOutsideBar() => 
    close < low[1] and high > high[1]


isSignalCandle() => 
    if  i_pyramiding or strategy.position_size == 0
        i_takeLongOrShort == "Long" ? bearishOutsideBar() : bullishOutsideBar()
    else
        false

idTrade() =>
	str.tostring(id)

roundUp(number, decimals) =>
    factor = math.pow(10, decimals)
    math.ceil(number * factor) / factor

positionSize(entryPrice, diff) =>
    float posSize = na
    if i_positionSize == "Fixed 1%"
        posSize := math.abs(roundUp(strategy.initial_capital *  0.01 / diff / syminfo.pointvalue, 2))
    else if i_positionSize == "Compound 1%"
        posSize := roundUp(strategy.equity *  0.01 / diff / syminfo.pointvalue, 2)
    else if i_positionSize == "1 Unit"
        posSize := 1

isCloseTime() =>
    closeHour = i_timeClosePM / 100
    closeMinute = i_timeClosePM % 100
    currentTime = timestamp(year(time), month(time), dayofmonth(time), closeHour, closeMinute, second(0))
    if i_isCloseTime == "End of session" and time[1] < currentTime and time >= currentTime
        true
    else if i_isCloseTime == "Friday end of session" and dayofweek(time) == dayofweek.friday and time[1] < currentTime and time >= currentTime
        true
    else
        false

isOpenTime() =>
    openHour = i_timeOpenAM / 100
    openMinute = i_timeOpenAM % 100
    currentTime = timestamp(year(time), month(time), dayofmonth(time), openHour, openMinute, second(0))
    if time[1] < currentTime and time >= currentTime
        true
    else
        false


//long
if isSignalCandle() and not isOpenTime()and i_takeLongOrShort == "Long" and barstate.isconfirmed

    // Basic calculcations
    longEntryPrice := low[1]
    longSL := longEntryPrice - (1 * (high - longEntryPrice))
    risk := longEntryPrice - longSL
    longTP := longEntryPrice + (i_rewardRatio * risk)

    // Position Size
    positionSize = positionSize(longEntryPrice, risk)
    
    // Do risk check
    if(risk > i_minRisk) 
        // Cancel order
        if i_cancellimitNewSignal
            strategy.cancel(idTrade())
            alert(i_pcLicenseID + ',cancelorder,' + syminfo.ticker + ',magic=' + idTrade() + ',strategytype=4')

        // Do entry
        id := math.floor(time("") / 100000)
        strategy.entry(id = idTrade(), direction = strategy.long, comment = idTrade(), stop = longEntryPrice, qty = positionSize, alert_message = i_pcLicenseID + ',entry,' + syminfo.ticker + ',strategytype=4')
        strategy.exit(idTrade(), from_entry =  idTrade(), comment= idTrade(), comment_loss=str.tostring("-1"), comment_profit = str.tostring(i_rewardRatio), stop=longSL, limit=longTP, alert_profit = i_pcLicenseID + ',tphit,' + syminfo.ticker + ',exitrr=' + str.tostring(i_rewardRatio) + ',strategytype=4', alert_loss = i_pcLicenseID + ',slhit,' + syminfo.ticker + ',exitrr=-1,strategytype=4')
        alert(i_pcLicenseID + ',buystop,' + syminfo.ticker + ',entryexpr=Bar[' + str.tostring(time[1]) + '].Low,risk=' + str.tostring(risk) + ',rr=' + str.tostring(i_rewardRatio) + ',magic=' + idTrade() + ',entryprice=' + str.tostring(longEntryPrice) + ',sl=' + str.tostring(longSL) + ',tp=' + str.tostring(longTP) + ',strategytype=4', alert.freq_once_per_bar_close)


if i_takeLongOrShort == "Long" and strategy.position_size > 0 and longEntryPrice != longSL and i_moveSLtoBEafterR > 0 and close >= (longEntryPrice + (i_moveSLtoBEafterR * risk)) and barstate.isconfirmed
    longSL := longEntryPrice
    strategy.exit(idTrade(), from_entry = idTrade(), comment = idTrade(), comment_loss= str.tostring("0"), comment_profit = str.tostring(i_rewardRatio), stop = longSL, limit=longTP, alert_loss = i_pcLicenseID + ',behit,' + syminfo.ticker + ',exitrr=0,strategytype=4', alert_profit = i_pcLicenseID + ',tphit,' + syminfo.ticker + ',exitrr= ' + str.tostring(i_rewardRatio) + ',strategytype=4') 


if isSignalCandle() and not isOpenTime() and i_takeLongOrShort == "Short" and barstate.isconfirmed

    // Basic calculcations
    shortEntryPrice := high[1]
    shortSL := shortEntryPrice + (1 * (shortEntryPrice - low))
    risk := shortSL - shortEntryPrice
    shortTP := shortEntryPrice - (i_rewardRatio * risk)


    // Position Size
    positionSize = positionSize(shortEntryPrice, risk)

    // Do risk check
    if(risk > i_minRisk) 
    
        // Cancel previous order
        if i_cancellimitNewSignal
            strategy.cancel(idTrade())
            alert(i_pcLicenseID + ',cancelorder,' + syminfo.ticker + ',magic=' + idTrade() + ',strategytype=4')

        // Do entry
        id := math.floor(time("") / 100000)
        strategy.entry(id = idTrade(), direction = strategy.short, comment = idTrade(), stop = shortEntryPrice, qty = positionSize, alert_message = i_pcLicenseID + ',entry,' + syminfo.ticker + ',strategytype=4')
        strategy.exit(idTrade(), from_entry =  idTrade(), comment= idTrade(), comment_loss=str.tostring("-1"), comment_profit = str.tostring(i_rewardRatio), stop=shortSL, limit=shortTP, alert_profit = i_pcLicenseID + ',tphit,' + syminfo.ticker + ',exitrr=' + str.tostring(i_rewardRatio) + ',strategytype=4', alert_loss = i_pcLicenseID + ',slhit,' + syminfo.ticker + ',exitrr=-1,strategytype=4')
        alert(i_pcLicenseID + ',sellstop,' + syminfo.ticker + ',entryexpr=Bar[' + str.tostring(time[1]) + '].High,risk=' + str.tostring(risk) + ',rr=' + str.tostring(i_rewardRatio) + ',magic=' + idTrade() + ',entryprice=' + str.tostring(shortEntryPrice) + ',sl=' + str.tostring(shortSL) + ',tp=' + str.tostring(shortTP) + ',strategytype=4', alert.freq_once_per_bar_close)


if i_takeLongOrShort == "Short" and strategy.position_size < 0 and shortEntryPrice != shortSL and i_moveSLtoBEafterR > 0 and close <= (shortEntryPrice - (i_moveSLtoBEafterR * risk)) and barstate.isconfirmed
    shortSL := shortEntryPrice
    strategy.exit(idTrade(), from_entry = idTrade(), comment = idTrade(), comment_loss= str.tostring("0"), comment_profit = str.tostring(i_rewardRatio), stop = shortSL, limit=shortTP, alert_loss = i_pcLicenseID + ',behit,' + syminfo.ticker + ',exitrr=0,strategytype=4', alert_profit = i_pcLicenseID + ',tphit,' + syminfo.ticker + ',exitrr= ' + str.tostring(i_rewardRatio) + ',strategytype=4') 


if isCloseTime() and barstate.isconfirmed
    rr = roundUp((shortEntryPrice - close) / risk, 2)
    if i_takeLongOrShort == "Long"
        rr := roundUp((close - longEntryPrice) / risk, 2)
    if rr > i_rewardRatio
        rr := 0
    else if rr < -1
        rr := -1
    strategy.close_all(comment = str.tostring(rr), alert_message = i_pcLicenseID + ',closeall,' + syminfo.ticker + ',magic=' + idTrade() + ',exitrr=' + str.tostring(rr) + ',strategytype=4')
    for i = 0 to 10
        strategy.cancel(str.tostring(id[i]))



//PLOTS
barcolor(isSignalCandle() ? color.blue  : na, title = "Signal Candle")
plotshape(isSignalCandle(), style=shape.xcross, color = color.black, title = "Signal Candle")
bgcolor(isCloseTime() ? color.new(color.red, 80) : na)
bgcolor(isOpenTime() ? color.new(color.green, 80) : na)

L1 = plot(i_showRiskReward and strategy.position_size > 0 ? longEntryPrice : na, color=color.black, linewidth=1, style=plot.style_linebr, title="Long Entry Price")
L2 = plot(i_showRiskReward and strategy.position_size > 0 ? longSL : na , color=color.red, linewidth=1, style=plot.style_linebr, title="Long Stop-Loss Price")
L3 = plot(i_showRiskReward and strategy.position_size > 0 ? longTP : na, color=color.green, linewidth=1, style=plot.style_linebr, title="Long Take-Profit Price")
fill(L1,L2,color=color.new(color.red,90))
fill(L1,L3,color=color.new(color.green,90)) 

S1 = plot(i_showRiskReward and strategy.position_size < 0 ? shortEntryPrice : na, color=color.black, linewidth=1, style=plot.style_linebr, title="Short Entry Price")
S2 = plot(i_showRiskReward and strategy.position_size < 0 ? shortSL : na , color=color.red, linewidth=1, style=plot.style_linebr, title="Short Stop-Loss Price")
S3 = plot(i_showRiskReward and strategy.position_size < 0 ? shortTP : na, color=color.green, linewidth=1, style=plot.style_linebr, title="Short Take-Profit Price")
fill(S1,S2,color=color.new(color.red,90))
fill(S1,S3,color=color.new(color.green,90)) 

var table nameDisplay = table.new(position.middle_center, 1, 1, bgcolor = color.white, frame_width = 0)
if barstate.islast and math.round(strategy.equity,0) <= 0
    table.cell(nameDisplay, 0, 0, "Total Equity dropped below ZERO.\nThese parameters are not useful.",text_color=color.white, bgcolor=color.red)

