//@version=5
strategy("STR6.1", overlay=true, process_orders_on_close = true, calc_on_every_tick = true, default_qty_type = strategy.percent_of_equity, default_qty_value = 100, pyramiding = 100, currency = currency.NONE, initial_capital = 10000000, close_entries_rule="ANY")
import TradingView/ta/5
import Jury_P/Tradehubler/1 as th


//INPUTS
i_showStrategy              = input.bool(true,title="Show Strategy",group = "General") 
i_showRiskReward            = input.bool(true,title="Show Risk/Reward Area",group = "General") 
i_takeLongOrShort           = input.string("Long", title="Direction",group = "General", options = ["Long", "Short"])

i_isCloseTime               = input.string("Friday end of session", title="Close trades",group = "Timing", options = ["Friday end of session", "End of session", "None"])
i_timeClosePM               = input.int(1455, title="Close Time",group = "Timing")

i_pyramiding                = input.bool(false,title = "Pyramiding?", group = "Risk / Reward")
i_rrType                    = input.string("Next candle close", title = "RR Type", options = ["RR", "Next candle close"], group = "Risk / Reward")
i_rewardRatio               = input.float(5.0, title = "Reward Ratio", group = "Risk / Reward") 
i_moveSLtoBEafterR          = input.float(0.0, title="Move SL to BE after xR",group = "Risk / Reward") 
i_cancellimitNewSignal      = input.bool(true, title="Cancel stop order when new signal",group = "Risk / Reward") 

i_checkTrend                = input.bool(false,title = "Only take trades with the trend?", group = "Trend") 
i_checkTrendTF              = input.string("Chart", title="Source", group = "Trend", options = ["Chart", "D", "720", "480", "240", "60", "15"])
i_checkTrendFastMA          = input.int(9, title="Fast MA", group = "Trend")
i_checkTrendslowMA          = input.int(24, title="Slow MA", group = "Trend")

i_pcLicenseID               = input.string("692803787",title="License ID",group = "Account")

//VARS
var int magic = 0
var float TP = na, var float SL = na, var float EP = na
var float risk = na
var int TIME = na

//FUNCTIONS

// Calculate moving averages
fast_ma = request.security(syminfo.tickerid, i_checkTrendTF == "Chart" ? timeframe.period : i_checkTrendTF, ta.ema(close, i_checkTrendFastMA))
slow_ma = request.security(syminfo.tickerid, i_checkTrendTF == "Chart" ? timeframe.period : i_checkTrendTF, ta.ema(close, i_checkTrendslowMA))

buyWithTrend() =>
    if i_checkTrend == true
        if fast_ma > slow_ma
            true
        else
            false
    else
        true

sellWithTrend() =>
    if i_checkTrend == true
        if fast_ma < slow_ma
            true
        else
            false
    else
        true

isBullishSetup() =>
    if i_takeLongOrShort == "Long"
        if  i_pyramiding or strategy.position_size == 0
            if buyWithTrend()
                low[1] < low[2] and low[1] < low[0]
            else
                false
        else
            false
    else
        false

isBearishSetup() =>
    if i_takeLongOrShort == "Short"
        if  i_pyramiding or strategy.position_size == 0
            if sellWithTrend()
                high[1] > high[2] and high[1] > high[0]
            else
                false
        else
            false
    else
        false



isCloseTime() =>
    if i_isCloseTime == "End of session" and th.isEndOfSession(i_timeClosePM)
        true
    else if i_isCloseTime == "Friday end of session" and th.isFridayEndOfSession(i_timeClosePM)
        true
    else
        false



//LONG
if isBullishSetup() and barstate.isconfirmed
    EP := high
    SL := low
    risk := EP - SL

    if i_cancellimitNewSignal
        th.cancelOrder(i_pcLicenseID, th.magicAsString(magic), 6)
    magic := th.magic()
    TP := i_rrType == "Next candle close" ? na : th.calculateTakeProfitForLong(EP, SL, i_rewardRatio)

    // reset entry time
    TIME := na

    // Do entry
    if strategy.equity > 0 and  th.positionSize(EP, SL, 1) <= 100000000000
        if i_rrType == "Next candle close"
            th.buyStop(i_pcLicenseID, th.magicAsString(magic), 6, i_rewardRatio, 1, EP, 'Bar[' + str.tostring(time[0]) + '].High', SL)
        else
            th.buyStop(i_pcLicenseID, th.magicAsString(magic), 6, i_rewardRatio, 1, EP, 'Bar[' + str.tostring(time[0]) + '].High', SL, TP)

if i_takeLongOrShort == "Long" and strategy.position_size > 0 and EP != SL and i_moveSLtoBEafterR > 0 and close >= (EP + (i_moveSLtoBEafterR * risk)) and barstate.isconfirmed
    SL := EP
    th.moveSlToBe(i_pcLicenseID, th.magicAsString(magic), 6, i_rewardRatio, SL, TP)

if i_rrType == "Next candle close"  and i_takeLongOrShort == "Long" and strategy.position_size > 0  and na(TIME) and barstate.isconfirmed
    TIME := time

if i_rrType == "Next candle close"  and i_takeLongOrShort == "Long" and strategy.position_size > 0  and time > TIME and barstate.isconfirmed
    rr = (close - EP) / risk
    th.closeOrder(i_pcLicenseID, th.magicAsString(magic), 6, rr)


// SHORT    
if isBearishSetup() and barstate.isconfirmed
    EP := low
    SL := high
    risk := SL - EP

    if i_cancellimitNewSignal
        th.cancelOrder(i_pcLicenseID, th.magicAsString(magic), 6)
    magic := th.magic()
    TP := i_rrType == "Next candle close" ? na : th.calculateTakeProfitForShort(EP, SL, i_rewardRatio)

    // reset entry entry time
    TIME := na
    
    // Do entry
    if strategy.equity > 0 and  th.positionSize(EP, SL, 1) <= 1000000000000
        if i_rrType == "Next candle close"
            th.sellStop(i_pcLicenseID, th.magicAsString(magic), 6, i_rewardRatio, 1, EP, 'Bar[' + str.tostring(time[0]) + '].Low', SL)
        else
            th.sellStop(i_pcLicenseID, th.magicAsString(magic), 6, i_rewardRatio, 1, EP, 'Bar[' + str.tostring(time[0]) + '].Low', SL, TP)


if i_takeLongOrShort == "Short" and strategy.position_size < 0 and EP != SL and i_moveSLtoBEafterR > 0 and close <= (EP - (i_moveSLtoBEafterR * risk)) and barstate.isconfirmed
    SL := EP
    th.moveSlToBe(i_pcLicenseID, th.magicAsString(magic), 6, i_rewardRatio, SL, TP)

if i_rrType == "Next candle close"  and i_takeLongOrShort == "Short" and strategy.position_size < 0  and na(TIME) and barstate.isconfirmed
    TIME := time

if i_rrType == "Next candle close"  and i_takeLongOrShort == "Short" and strategy.position_size < 0  and time > TIME and barstate.isconfirmed
    rr = (EP - close) / risk
    th.closeOrder(i_pcLicenseID, th.magicAsString(magic), 6, rr)
 
if isCloseTime() and barstate.isconfirmed
    rr = th.roundUp((EP - close) / risk, 2)
    if i_takeLongOrShort == "Long"
        rr := th.roundUp((close - EP) / risk, 2)
    if rr > i_rewardRatio
        rr := 0
    else if rr < -1
        rr := -1
    th.closeAllOrders(i_pcLicenseID, th.magicAsString(magic), 6, rr)
    strategy.cancel(id = th.magicAsString(magic))





//PLOTS
barcolor(isBullishSetup() ? color.blue  : na, title = "Signal Candle")
plotshape(isBullishSetup(), style=shape.xcross, color = color.black, title = "Signal Candle")
barcolor(isBearishSetup() ? color.blue  : na, title = "Signal Candle")
plotshape(isBearishSetup(), style=shape.xcross, color = color.black, title = "Signal Candle")
plot(i_checkTrend ? fast_ma : na, color=color.blue)
plot(i_checkTrend ? slow_ma : na, color=color.red)

L1 = plot(i_showRiskReward and strategy.position_size > 0 ? EP : na, color=color.black, linewidth=1, style=plot.style_linebr, title="Long Entry Price")
L2 = plot(i_showRiskReward and strategy.position_size > 0 ? SL : na , color=color.red, linewidth=1, style=plot.style_linebr, title="Long Stop-Loss Price")
L3 = plot(i_showRiskReward and strategy.position_size > 0 ? TP : na, color=color.green, linewidth=1, style=plot.style_linebr, title="Long Take-Profit Price")
fill(L1,L2,color=color.new(color.red,90))
fill(L1,L3,color=color.new(color.green,90)) 

S1 = plot(i_showRiskReward and strategy.position_size < 0 ? EP : na, color=color.black, linewidth=1, style=plot.style_linebr, title="Short Entry Price")
S2 = plot(i_showRiskReward and strategy.position_size < 0 ? SL : na , color=color.red, linewidth=1, style=plot.style_linebr, title="Short Stop-Loss Price")
S3 = plot(i_showRiskReward and strategy.position_size < 0 ? TP : na, color=color.green, linewidth=1, style=plot.style_linebr, title="Short Take-Profit Price")
fill(S1,S2,color=color.new(color.red,90))
fill(S1,S3,color=color.new(color.green,90)) 

var table nameDisplay = table.new(position.middle_center, 1, 1, bgcolor = color.white, frame_width = 0)
if barstate.islast and math.round(strategy.equity,0) <= 0
    table.cell(nameDisplay, 0, 0, "Total Equity dropped below ZERO.\nThese parameters are not useful.",text_color=color.white, bgcolor=color.red)
